// removes good/evil axis alignment restrictions from all cleric spells except those specifically called out
// description adjustment is heuristic, based on the presence of a set of strings (10001-10020 in the tra file,
// by default dw_iwdspells_divine.tra)

DEFINE_ACTION_FUNCTION dealign_cleric_spells STR_VAR version=1 BEGIN

	// load dialog strings
	
	ACTION_IF version=1 BEGIN
	  ACTION_CLEAR_ARRAY dealign_dialog_array
	  OUTER_FOR (line=1;line<=20;++line) BEGIN
		OUTER_SET tra_ref=line+10000
		OUTER_SPRINT $dealign_dialog_array("%line%") (AT tra_ref)
	  END
	END
	
	// load special cases
	ACTION_CLEAR_ARRAY dealign_exclude
	ACTION_FOR_EACH special_spell IN CLERIC_HOLY_WORD CLERIC_UNHOLY_WORD CLERIC_HOLY_SMITE CLERIC_UNHOLY_BLIGHT BEGIN
		LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name="%special_spell%" RET spell_res END
		ACTION_TO_UPPER spell_res
		OUTER_SPRINT $dealign_exclude("%spell_res%") ""
	END
	
	// main pass through spells
	
	COPY_EXISTING_REGEXP "sppr[1-7][0-9][0-9]\.spl" override
		SPRINT filename "%SOURCE_RES%"
		TO_UPPER filename
		PATCH_IF !VARIABLE_IS_SET $dealign_exclude("%filename%") BEGIN
			// the actual alignment shift (much the easiest bit!)
			READ_BYTE 0x1e align_byte_old
			align_byte_new= align_byte_old BAND 0b11110001
			PATCH_IF !(align_byte_new=align_byte_old) BEGIN
				WRITE_BYTE 0x1e align_byte_new
				PATCH_IF version=1 BEGIN
				  READ_LONG 0x50 desc_strref
				  LPF dealign_cleric_spell_description INT_VAR desc_strref END
				END
			END
		END
	BUT_ONLY

END

DEFINE_PATCH_FUNCTION dealign_cleric_spell_description INT_VAR desc_strref=0 BEGIN

  PATCH_IF desc_strref>0 BEGIN
	GET_STRREF desc_strref desc
	INNER_PATCH_SAVE desc "%desc%" BEGIN
		FOR (line=1;line<=20;++line) BEGIN
			SPRINT string $dealign_dialog_array("%line%")
			REPLACE_TEXTUALLY "\.[^.]*%string%[^.]*\." "."
		END
	END
	INNER_ACTION BEGIN
		STRING_SET_EVALUATE desc_strref "%desc%"
	END
  END

END