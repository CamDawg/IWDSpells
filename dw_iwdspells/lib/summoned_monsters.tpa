/// operates *after* primary converter run, to sort out summons


DEFINE_ACTION_FUNCTION summoned_monsters_arcane BEGIN

    LAM read_in_saving_throws

/////////////////////////////////////////////////////
//// Create Boneguard
///////////////////////////////////////////////////

  LAF check_ini STR_VAR ini=create_boneguard RET value END
  ACTION_IF value BEGIN

    CLEAR_IDS_MAP
    LAF get_spellcode STR_VAR input=WIZARD_MONSTER_SUMMONING_7 RET spell=value END
    COPY_EXISTING "%spell%.spl" "%workspace%/boneguard.spl"
    MAKE_PATCH
       say_name=>1
       say_description=>2
       enforce_school=>necromancy
       patch_effect_inline=>"opcode=>177 resource=>dw#spbgd parameter2=>2 probability1=>50 dicenum=>0 dicesize=>0"
       clone_effect_inline=>"number_to_add=>2 probability1=>100 probability2=>51"
    END
    LAF install_spell STR_VAR idsname=WIZARD_CREATE_BONEGUARD spell="boneguard" locabs="%workspace%" edits=patch_data RET WIZARD_CREATE_BONEGUARD=newname END
    LAF collect_spell_icons STR_VAR location=bam locbase=hla resref=tg#bone spell=WIZARD_CREATE_BONEGUARD END
    
    // at least pro tem, steal Protection from Undead scroll icon

    COPY_EXISTING iscrl09.bam "override/%WIZARD_CREATE_BONEGUARD%A.bam"

    // sort out scroll

      LAF find_scroll_slot RET scroll_slot END
      OUTER_SPRINT scroll_new "SCRL%scroll_slot%"

    MAKE_PATCH
       say_both_names=>1
       say_both_descriptions=>2
       patch_effect_inline=>"resource=>%WIZARD_CREATE_BONEGUARD%"
       patch_ability_inline=>"ability_icon=>%WIZARD_CREATE_BONEGUARD%A"
       icon=>"%WIZARD_CREATE_BONEGUARD%A"
       unusable_invoker=>0
       unusable_diviner=>0
       unusable_illusionist=>1
    END
    LAF clone_item STR_VAR item="%WIZARD_MONSTER_SUMMONING_7_SCROLL%=>%scroll_new%" edits=patch_data END


   OUTER_SPRINT shadow "%WIZARD_WAIL_OF_THE_BANSHEE_SCROLL%"
   MAKE_PATCH
      clone_item_inline=>"match=>~item_resource=%shadow%~ item_resource=>%scroll_new%"
   END
   COPY_EXISTING_REGEXP GLOB ".*\.sto" override
           PATCH_IF INDEX_BUFFER ("%shadow%")>=0 BEGIN
              LPF apply_patches STR_VAR edits=patch_data file_ext=STO END
           END
   BUT_ONLY

    // make effect

    MAKE_PATCH
       duration=>2400
       timing=>1
       resource=>ms7bgrd
       resource2=>spraised
       target=>1
       opcode=>67
    END
    LAF make_effect STR_VAR effect=dw#spbgd edits=patch_data END

    // tweak monster file

    MAKE_PATCH
      delete_effect=>"opcode=233"
      swap_script=>"gnsummm=>bdsumm00"
      xp_value=>0
    END
    LAF edit_creature STR_VAR creature=ms7bgrd edits=patch_data END
  
  END  

/////////////////////////////////////////////////////
//// MS7
///////////////////////////////////////////////////

//// Umber hulk - baseline is IWDEE's MS7 UH but it needs BG2-style immunities and confusion

   MAKE_PATCH
      delete_effect=>"opcode=233" // pointless cruft in IWD file
      remove_items=>umberhlk
      add_item=>"helmnoan umber01"
      patch_item_inline=>"unstealable=>1 undroppable=>1"
      swap_script=>"UHGAZE=>DW#UMBER GNSUMMM=>BDSUMM00"
      dv=>ms7umber
      xp_value=>0
   END
   LAF edit_creature STR_VAR creature=MS7UMBH edits=patch_data END
   LAF install STR_VAR file=dw#umber.baf location="%iwdspells_resource%" END

//// Ettin - baseline is BG2 ettin

   MAKE_PATCH
       allegiance=>controlled
       gender=>summoned
       dv=>ms7ettin
       no_corpse=>1
       swap_script=>"wtasight=>BDSUMM00"
       xp_value=>0
   END
   LAF clone_creature STR_VAR creature="PETTIN=>DW#MS7ET" edits=patch_data END

   LAF make_summoning_2da STR_VAR monsters="MS7UMBH DW#MS7ET" filename=msummo7 END

/////////////////////////////////////////////////////
//// MS6
///////////////////////////////////////////////////

/// Frost salamander  - base is IWDEE's MS6 salamander but needs BG2-style aura etc

     MAKE_PATCH
          delete_effect=>"opcode=233"
          add_item=>"ring95 immune1"
          class=>ELEMENTAL_WATER // best match - no SALAMANDER class in BG2
          dv=>MS6salac
          remove_items=>salfros
          add_item=>salgrice
          strip_script=>safrost
          patch_item_inline=>"unstealable=>1 undroppable=>1"
          add_proficiencies=>"spear=>2"
          swap_script=>"gnsummm=>bdsumm00"
          xp_value=>0
     END
     LAF edit_creature STR_VAR creature=MS6SALC edits=patch_data END

//// Fire salamanader - ditto

     MAKE_PATCH
          delete_effect=>"opcode=233"
          add_item=>"ring95 immune1"
          class=>ELEMENTAL_FIRE// best match - no SALAMANDER class in BG2
          dv=>MS6salaf
          remove_items=>salfire
          add_item=>salgrfir
          strip_script=>safire
          patch_item_inline=>"unstealable=>1 undroppable=>1"
          add_proficiencies=>"spear=>2"
          swap_script=>"gnsummm=>bdsumm00"
         xp_value=>0
     END
     LAF edit_creature STR_VAR creature=MS6SALF edits=patch_data END

//// giant troll - base is BG2 trolgi01

     MAKE_PATCH
       allegiance=>controlled
       gender=>summoned
       dv=>ms6gttrl
       no_corpse=>1
       swap_script=>"wtasight=>bdsumm00"
       strip_script=>gensht01
       xp_value=>0
     END
     LAF clone_creature STR_VAR creature="trolgi01=>DW#MS6GT" edits=patch_data END

     LAF make_summoning_2da STR_VAR monsters="MS6SALC MS6SALF DW#MS6GT" filename=msummo6 END


/////////////////////////////////////////////////////
//// MS5
///////////////////////////////////////////////////

  /// ordinary troll

     MAKE_PATCH
       allegiance=>controlled
       gender=>summoned
       dv=>ms5troll
       no_corpse=>1
       swap_script=>"wtatroll=>bdsumm00"
       xp_value=>0
     END
     LAF clone_creature STR_VAR creature="troll01=>DW#MS5TR" edits=patch_data END

 /// minotaur
 
     MAKE_PATCH
        delete_effect=>"opcode=233"
        immunity_to_spell=>"%WIZARD_MAZE% %PSIONIC_MAZE%"
        remove_items=>"s1-8m1"
        add_items=>"ax1h01"
        patch_item_inline=>"unstealable=>1 undroppable=>1"
        add_proficiencies=>"axe=>2"
        strength=>18
        str_ex=>100
        xp_value=>0
          swap_script=>"gnsummm=>bdsumm00"
     END
     LAF edit_creature STR_VAR creature=ms5mino edits=patch_data END
     

     LAF make_summoning_2da STR_VAR monsters="MS5MINO DW#MS5TR" filename=msummo5 END


/////////////////////////////////////////////////////
//// MS4
///////////////////////////////////////////////////

     // ogre mage - based on unused monster-summon in BG2

     LAF ds_resolve_stat INT_VAR ids=1 STR_VAR id=ITEM_MM RET stat_ind END
     LAF ds_resolve_stat INT_VAR ids=1 STR_VAR id=ITEM_MM_SEEN RET stat_ind END
     LAF ds_resolve_stat INT_VAR ids=1 STR_VAR id=DO_NOT_TARGET_SPELLS RET stat_ind END
     LAF ssl_to_bcs STR_VAR location="%iwdspells_resource%" script=ogremasu END

     MAKE_PATCH
          allegiance=>controlled
          no_corpse=>1
          dv=>ms4ogrem
          add_proficiencies=>"singleweapon=>2 bastardsword=>2"
          remove_spells=>all
          add_spells=>"MAGIC_MISSILE(2) BURNING_HANDS(2) MIRROR_IMAGE MELF_ACID_ARROW LIGHTNING_BOLT GHOST_ARMOR"
          swap_script=>"wtasight=>bdsumm00"
          xp_value=>0
     END
     LAF edit_creature STR_VAR creature=ogremasu edits=patch_data END

     /// yuan-ti

     MAKE_PATCH
       allegiance=>controlled
       gender=>summoned
       dv=>ms5yuant
       no_corpse=>1
       swap_script=>"wtasight=>bdsumm00"
       xp_value=>0
     END
     LAF clone_creature STR_VAR creature="icyuan01=>DW#MS4YU" edits=patch_data END

     LAF make_summoning_2da STR_VAR monsters="OGREMASU DW#MS4YU" filename=msummo4 END


/////////////////////////////////////////////////////
//// MS3
///////////////////////////////////////////////////

    // ogre berserker

    MAKE_PATCH
       enforce_class=>null
       remove_items=>ogre1
       add_items=>blun06
       add_proficiencies=>"FLAILMORNINGSTAR=>2"
       patch_item_inline=>"unstealable=>1 undroppable=>1"
       insert_script_high=>"dw#msogr"
       no_corpse=>1
       xp_value=>0
    END
    LAF edit_creature STR_VAR creature=ogregrsu edits=patch_data END
    LAF install STR_VAR file=dw#msogr.baf location="%iwdspells_resource%" END

    // hobgoblin elite
    MAKE_PATCH
       add_proficiencies=>"BASTARDSWORD=>2 LONGBOW=>2"
       no_corpse=>1
       xp_value=>0
    END
    LAF edit_creature STR_VAR creature=hobgobsu edits=patch_data END

    // ettercap

    MAKE_PATCH
       no_corpse=>1
       xp_value=>0
    END
    LAF edit_creature STR_VAR creature=ettercsu edits=patch_data END

    LAF make_summoning_2da STR_VAR monsters="OGREGRSU ETTERCSU HOBGOBSU" filename=msummo3 END


/////////////////////////////////////////////////////
//// MS2
///////////////////////////////////////////////////

    // gnoll
    
    MAKE_PATCH
       level=>3
       name1_string=>6627
       name2_string=>6627
       hitpoints=>24
       attacks=>1
       no_corpse=>1
       add_proficiencies=>"spear=>1"
       xp_value=>0
    END
    LAF edit_creature STR_VAR creature=gnollsu edits=patch_data END
    
    // ogrillon
    
    MAKE_PATCH
        attacks=>1
        no_corpse=>1
        xp_value=>0
    END
    LAF edit_creature STR_VAR creature=ogrelesu edits=patch_data END
    
    // lizard man
    
    MAKE_PATCH
        xp_value=>0
        level=>3
       swap_script=>"gnsummm=>bdsumm00"
    END
    LAF edit_creature STR_VAR creature=ms2lizm edits=patch_data END

    // ogre

    MAKE_PATCH
        no_corpse=>1
       swap_script=>"gnsummm=>bdsumm00"
    END
    LAF edit_creature STR_VAR creature=ogresu edits=patch_data END

    LAF make_summoning_2da STR_VAR monsters="MS2LIZM OGRELESU GNOLLSU OGRESU" filename=msummo2 END

/////////////////////////////////////////////////////
//// MS1
///////////////////////////////////////////////////

    // goblins
    
    MAKE_PATCH
       patch_item_inline=>"unstealable=>1 undroppable=>1"
       xp_value=>0
       swap_script=>"gnsummm=>bdsumm00"
       swap_script'=>"gnsummb=>bdsumm00"
    END
    LAF edit_creature STR_VAR creature="ms1goba ms1gobm" edits=patch_data END

    // hobgoblins
    
    MAKE_PATCH
       gender=>summoned
       no_corpse=>1
       allegiance=>controlled
       xp_value=>0
       dv=>ms1hobgo
       strip_script=>"gensht01 hobarc01"
       swap_script=>"wtasight=>bdsumm00"
       patch_item_inline=>"unstealable=>1 undroppable=>1"
    END
    LAF clone_creature STR_VAR creature="hobwar01=>dw#ms1hw hobarc01=>dw#ms1ha" edits=patch_data END

    // orc archer
    MAKE_PATCH
       gender=>summoned
       level=>1
       hitpoints=>8
       thac0=>20
       attacks=>1
       no_corpse=>1
       allegiance=>controlled
       xp_value=>0
       dv=>ms1orcar
       strip_script=>"gensht01 oarch02"
       swap_script=>"wtasight=>bdsumm00 wtarsgt=>bdsumm00"
       animation_code=>59408
       remove_items=>rndtre03
       patch_item_inline=>"unstealable=>1 undroppable=>1"
       patch_item_inline'=>"match=>~resource=arow02~ resource=>arow01"
       enforce_saves=>null
    END
    LAF clone_creature STR_VAR creature="orc02=>dw#ms1oa" edits=patch_data END
    
    // orc warrior
    MAKE_PATCH
       gender=>summoned
       level=>1
       hitpoints=>8
       thac0=>20
       attacks=>1
       no_corpse=>1
       allegiance=>controlled
       xp_value=>0
       dv=>ms1orcwa
       strip_script=>"gensht01 orcwar"
       swap_script=>"wtasight=>bdsumm00 wtarsgt=>bdsumm00"
       remove_items=>rndtre03
       patch_item_inline=>"unstealable=>1 undroppable=>1"
       enforce_saves=>null
    END
    LAF clone_creature STR_VAR creature="orc01=>dw#ms1ow" edits=patch_data END

    LAF make_summoning_2da STR_VAR monsters="DW#MS1OW DW#MS1OA DW#MS1HW DW#MS1HA MS1GOBA MS1GOBM" filename=msummo1 END

/////////////////////////////////////////////////////
//// shadow summons
///////////////////////////////////////////////////

   // the shadow umber hulk needs BGification

   MAKE_PATCH
      remove_items=>umberhlk
      add_item=>"helmnoan umber01"
      patch_item_inline=>"unstealable=>1 undroppable=>1"
      swap_script=>"UHGAZE=>DW#UMBER GNSUMMM=>BDSUMM00"
      xp_value=>0
   END
   LAF edit_creature STR_VAR creature="SS3UMB8 SS3UMB9" edits=patch_data END


/////////////////////////////////////////////////////
//// misc
///////////////////////////////////////////////////


   // everything needs BD scripts

   MAKE_PATCH
      xp_value=>0
      swap_script=>"GNSUMMM=>BDSUMM00"
   END
   LAF edit_creature STR_VAR creature="SUMSHAD SS1GOB1 SS1GOB2 SS1GOB3 SS1LIZ3 SS1LIZ4 SS1TRL6 SS1TRL7 SS1TRL8 SS2GOB1 SS2GOB2 SS2GOB3 SS2LIZ5 SS2LIZ6 SS2LIZ7 SS3TRL7 SS3TRL8" edits=patch_data END // Summon Shadow
   LAF edit_creature STR_VAR creature="ES8WATR ES8FIRE ES8ERTH ES8AIR" edits=patch_data END // elementals
   LAF edit_creature STR_VAR creature="AD3SKLM" edits=patch_data END // soul eater



END

DEFINE_ACTION_FUNCTION summoned_monsters_divine BEGIN

/////////////////////////////////////////////////////
//// shamblers - put on a more standard format
///////////////////////////////////////////////////

   LAF clone_effect STR_VAR effect="SPEART1P=>DW#SHAMB" editstring="resource=>SSHAMB" END

   MAKE_PATCH
      match=>"opcode=67"
      opcode=>177
      target=>2
      parameter2=>2
      duration=>2400
      resource=>DW#SHAMB
   END
   LAF edit_spell STR_VAR spell="%CLERIC_STALKER%" editstring="patch_effect=>patch_data" END

   MAKE_PATCH
      allegiance=>controlled
      swap_script=>"EFATKMEL=>BDSUMM00"
   END
   LAF edit_creature STR_VAR creature=SSHAMB edits=patch_data END

/////////////////////////////////////////////////////
//// giant insect - just map over to new scripts
///////////////////////////////////////////////////

   MAKE_PATCH
      xp_value=>0
      swap_script=>"GNSUMMM=>BDSUMM00"
   END
   LAF edit_creature STR_VAR creature="GISBOMB GISBORB" edits=patch_data END



END

<<<<<<<< .../stratagems-inline/summon_template.2da
2DA V1.0
0
     RESREF	Hit 	AreaHitAnimation
>>>>>>>>

DEFINE_ACTION_FUNCTION make_summoning_2da 
   STR_VAR filename=""
           monsters=""
           anim_hit="msumm1h"
           anim_area="msumm1x"
BEGIN
   COPY ".../stratagems-inline/summon_template.2da" "override/%filename%.2da"
   OUTER_SET counter=1
   OUTER_WHILE "%monsters%" STRING_COMPARE "" BEGIN
      LAF return_first_entry STR_VAR list="%monsters%" RET entry monsters=list END
      APPEND "%filename%.2da" "%counter% %entry% %anim_hit% %anim_area%"
      OUTER_SET counter +=1
   END
END